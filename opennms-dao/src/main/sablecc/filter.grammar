Package org.opennms.netmgt.filter;

Helpers
all              = [0 .. 0xffff];
letter           = ['a' .. 'z'] | ['A' .. 'Z'];
number           = ['0' .. '9'];
white_space      = (' ' | 9 | 10 | 13)*;
name_part        = (letter | number | '-')*;
name             = letter name_part ( '_' name_part)*;
list             = (number* ',')* number*;
range            = number* '-' number*;
service_ident    = 'is' name;
notservice_ident    = 'notis' name;
catinc_ident    = 'catinc' name;

Tokens
semi             = ';';
not              = '!';
compare          = '==';
not_equal        = '!=';
gtr_than         = '>';
gtr_than_equal   = '>=';
less_than        = '<';
less_than_equal  = '<=';
and              = '&';
or               = '|';
like             = 'LIKE' | 'like';
tildelike        = '~';
iplike           = 'IPLIKE' | 'iplike';
lparen           = '(';
rparen           = ')';
blank            = white_space;
integer          = number*;
ident            = service_ident | name;
notident            = notservice_ident | name;
quoted_string    = '"' [all - '"']* '"' | ''' [all - ''']* ''';
dot              = '.';
star             = '*';
octet_list       = list;
octet_range      = range;
octet_range_list = range ',' list;
	
Ignored Tokens
blank;

Productions
rule = 	{and}         rule and expr |
	{or}          rule or  expr |
	{rule_expr}   expr;

expr =  {expr_part}   expr_part |
	{expr_parens} lparen rule rparen;

expr_part =  {ident}           ident |
	     {gtr_than}        ident gtr_than compare_right |
	     {less_than}       ident less_than compare_right |
	     {gtr_than_equal}  ident gtr_than_equal compare_right |
	     {less_than_equal} ident less_than_equal compare_right |
	     {compare}         ident compare compare_right |
	     {not_equal}       ident not_equal compare_right |
	     {not}             not expr |
	     {like}            ident like quoted_string |
	     {tildelike}       ident tildelike quoted_string |
	     {iplike}          ident iplike ip_ident;

compare_right = {integer} integer |
	        {string}  quoted_string;

ip_ident = {ipaddr} [oct1]:octet [dot1]:dot [oct2]:octet [dot2]:dot [oct3]:octet [dot3]:dot [oct4]:octet;

octet = {star}             star |
        {octet_list}       octet_list |
	{octet_range}      octet_range |
	{octet_range_list} octet_range_list |
	{integer}          integer;
